#!/usr/bin/env python

Import("env")
Import("env_modules")

env_tulip = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []
thirdparty_dir = "#thirdparty/yoga/"
thirdparty_sources = [
    "yoga/log.cpp",
    "yoga/Utils.cpp",
    "yoga/YGConfig.cpp",
    "yoga/YGEnums.cpp",
    "yoga/YGLayout.cpp",
    "yoga/YGNode.cpp",
    "yoga/YGNodePrint.cpp",
    "yoga/YGStyle.cpp",
    "yoga/YGValue.cpp",
    "yoga/Yoga.cpp",
    "yoga/event/event.cpp"
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_tulip.Prepend(CPPPATH=[thirdparty_dir])

env_thirdparty = env_tulip.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
env.modules_sources += thirdparty_obj

# Godot source files

module_obj = []

env_tulip.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
